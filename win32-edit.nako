#--------------------------------------------------------------------------
# なでしこPHP3編集プログラム(Windows版)
#--------------------------------------------------------------------------
#--------------------------------------------------------------------------
# 自由に変更等してください。（転載 引用自由）
# なでしこ3PHP変換・実行プログラムでなでしこ3PHPの初期設定を先にしてください。
#
# 2022年5月28日更新 EZNAVI.net
#※注意：絵文字は非対応(SJISのため)なので絵文字の命令などはエラーが出ます！
#--------------------------------------------------------------------------

PHP=「{母艦パス}php8\php.exe」
PHP_INI=「{母艦パス}php.ini」
PHPNAKO=「node "{母艦パス}node_modules\nadesiko3php\src\phpnako.js"」
NODE=「{母艦パス}nodejs16\node.exe」
NPM=「{母艦パス}nodejs16\npm.cmd」

PHPダウンロードURL=「https://windows.php.net/downloads/releases/archives/php-8.1.5-Win32-vs16-x86.zip」
NODEダウンロードURL=「https://nodejs.org/dist/v16.15.0/node-v16.15.0-win-x86.zip」


#--------------------------------------------------------------------------
#メニュー作成
#--------------------------------------------------------------------------
「
   ,ファイル
  -,ファイル操作1,開く
  -,ファイル操作2,名前を選択し保存
  -,ファイル操作3,上書保存
  -,ファイル操作4,ファイル先確認
   ,フォルダ操作,フォルダ
  -,フォルダ確認,フォルダ表示
  -,監視モード,フォルダ監視
   ,サーバー対応,サーバー
  -,ローカルサーバー確認,サーバー表示
  -,ローカルサーバー立上,サーバー立上

   ,サンプル
  -,サンプル１,掲示板
  -,サンプル２,チャット
  -,サンプル３,その他
   ,リンク
  -,リンク公式,なでしこ３公式
  -,リンクPHP,なでしこPHP
  -,リンク貯蔵庫,なでしこ３貯蔵庫
  -,リンクチャンネル,公式動画チャンネル
   ,閉じる
 」をメニュー一括作成

#--------------------------------------------------------------------------
#母艦・エディタ準備
#--------------------------------------------------------------------------
母艦を最大化
母艦のタイトル＝「なでしこ3PHP - 編集ツール」
PHPエディタとはTエディタ
そのW=母艦のW-50
そのH=母艦のH-80
編集資料＝「」 

#--------------------------------------------------------------------------
#Edgeでローカルサーバーを表示
#--------------------------------------------------------------------------
#※http://localhost:8888/で表示します
ローカルサーバー確認のクリックした時は
 「cmd.exe /c start Microsoft-Edge:http://localhost:8888/」を起動

#--------------------------------------------------------------------------
#ローカルサーバー立ち上げ
#--------------------------------------------------------------------------
#※http://localhost:8888/で表示します
ローカルサーバー立上のクリックした時は
　指定フォルダ＝「{作業フォルダ}」
　「cmd.exe /k cd "｛指定フォルダ｝"& "{PHP}" -S 0.0.0.0:8888 -c "{PHP_INI}"」を起動

#--------------------------------------------------------------------------
#ファイルをPHPに自動変換（監視モード）
#--------------------------------------------------------------------------
#※.nako3ファイルを編集保存すると自動でphpも変更されます。
監視モードのクリックした時は
　指定フォルダ＝「{作業フォルダ}」
　「cmd.exe /k cd "{指定フォルダ}"& {PHPNAKO} --watch」を起動

#--------------------------------------------------------------------------
#編集フォルダ確認として表示
#--------------------------------------------------------------------------
フォルダ確認のクリックした時は
　「cmd.exe /c start {作業フォルダ}」を起動



#--------------------------------------------------------------------------
#公式サイトのリンク
#--------------------------------------------------------------------------
リンク公式のクリックした時は
　「cmd.exe /c start Microsoft-Edge:https://nadesi.com/doc3/」を起動
リンクPHPのクリックした時は
　「cmd.exe /c start Microsoft-Edge:https://nadesi.com/doc3/index.php?nadesiko3php」を起動
リンク貯蔵庫のクリックした時は
　「cmd.exe /c start Microsoft-Edge:https://n3s.nadesi.com/」を起動
リンクチャンネルのクリックした時は
　「cmd.exe /c start Microsoft-Edge:https://www.youtube.com/channel/UCF78j9YzEunzh9ZLmaGC0mA」を起動

#--------------------------------------------------------------------------
#閉じる
#--------------------------------------------------------------------------
閉じるのクリックした時は
　終了

#--------------------------------------------------------------------------
#ファイル開く
#--------------------------------------------------------------------------
ファイル操作1のクリックした時は
　「.nako3」のファイル選択
　編集資料＝それ
　もし、(それの存在＝はい)ならば、
　　データ甲＝編集資料を開いてSJIS変換
　　PHPエディタにデータ甲を代入する
　　母艦のタイトル＝「なでしこ3PHP - 編集ツール-{編集資料}」
　違えば
　　#

#--------------------------------------------------------------------------
#名前選択して保存
#--------------------------------------------------------------------------
ファイル操作2のクリックした時は
　「.nako3」の保存ファイル選択
　保存資料=それ
　もし、保存資料=「」ならば、
　違えば
　　PHPエディタのテキストをUTF8N変換して「{保存資料}」に保存
　　編集資料=保存資料
　　母艦のタイトル＝「なでしこ3PHP - 編集ツール-{編集資料}」
　　「名前を付けて保存
{保存資料}」と言う

#--------------------------------------------------------------------------
#上書保存
#--------------------------------------------------------------------------
ファイル操作3のクリックした時は
　もし、編集資料=「」ならば、
　違えば
　　PHPエディタのテキストをUTF8N変換して「{編集資料}」に保存
　　母艦のタイトル＝「なでしこ3PHP - 編集ツール-{編集資料}」
　　「上書保存
{編集資料}」と言う

#--------------------------------------------------------------------------
#編集ファイル場所確認
#--------------------------------------------------------------------------
ファイル操作4のクリックした時は
　「{編集資料}」と言う


#--------------------------------------------------------------------------
#サンプル/掲示板/チャットなど
#--------------------------------------------------------------------------

サンプルダウンロードURL=「https://github.com/kujirahand/phpnako-exampe-bbs/archive/refs/heads/main.zip」
サンプル１をクリックした時は
　ZIPFILE=「{母艦パス}nade3phpbbs.zip」
　もし(ZIPFILEが存在)ならば
　　「cmd.exe /c start {母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/bbs」を起動
　違えば
　　「初回ダウンロード！
(SJIS＆絵文字対策済み)」と言う
　　サンプルダウンロードURLをZIPFILEへHTTPダウンロード。
　　ZIPFILEを「{母艦パス}nade3phpbbs」へ解凍。
　　「cmd.exe /c start {母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/bbs」を起動
　編集資料＝「{母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/bbs/index.nako3」
　データ乙＝編集資料を開く
#　データ乙を文字コード調査して言う
　データ乙＝データ乙をSJIS変換
　PHPエディタにデータ乙を代入する。
　
#SJIS&絵文字対策
　ナビ資料＝「https://www.eznavi.net/site/nade3php/sjis-bbs/index.nako3」
　ナビ資料を編集資料へHTTPダウンロード。
　編集資料＝「{母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/bbs/index.nako3」
　データ乙＝編集資料を開く
　データ乙＝データ乙をSJIS変換
　PHPエディタにデータ乙を代入する。


サンプル２をクリックした時は
　ZIPFILE=「{母艦パス}nade3phpbbs.zip」
　もし(ZIPFILEが存在)ならば
　　「cmd.exe /c start {母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/chat」を起動
　違えば
　　「初回ダウンロード！
(SJIS＆絵文字対策済み)」と言う
　　サンプルダウンロードURLをZIPFILEへHTTPダウンロード。
　　ZIPFILEを「{母艦パス}nade3phpbbs」へ解凍。
　　「cmd.exe /c start {母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/chat」を起動
　編集資料＝「{母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/chat/index.nako3」
　データ乙＝編集資料を開く
#データ乙を文字コード調査して言う
　データ乙＝データ乙をSJIS変換
　PHPエディタにデータ乙を代入する。
　
#SJIS&絵文字対策
　ナビ資料＝「https://www.eznavi.net/site/nade3php/sjis-chat/index.nako3」
　ナビ資料を編集資料へHTTPダウンロード。
　編集資料＝「{母艦パス}nade3phpbbs/phpnako-exampe-bbs-main/chat/index.nako3」
　データ乙＝編集資料を開く
　データ乙＝データ乙をSJIS変換
　PHPエディタにデータ乙を代入する。

　
サンプル３をクリックした時は
　PHPエディタのテキスト＝「今を表示。
PHPバージョンを表示。
ナデシコバージョンを表示。
」


